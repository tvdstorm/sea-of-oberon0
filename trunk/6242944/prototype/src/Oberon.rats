module Oberon;

header {
	import xtc.util.Action;
	import java.util.*;
}
// NOTE: a mess, need to clean this up

transient String Digit = [0-9];
transient String Letter = [A-Za-z];

transient void Space = ' ' / '\t' / '\f' / '\n' / '\r';
transient void Spacing = Space*;
transient void EndOfFile = !_ ;

String Ident = Letter (Letter / Digit)*;

transient String IntString = Digit+;


ast.IntegerLiteral Integer = 
	i:IntString
	{ yyValue = new ast.IntegerLiteral(new Integer(i)); }
	;

ast.Identifier Identifier = 
	ident:Ident Spacing
	{ yyValue = new ast.Identifier(ident); }
	;

//FIXME: use Action here?
ast.Expression IdentifierSelector = 
	ident:Identifier
	exprs:(void:"[" Expression void:"]")*
	{ 
		List<ast.Expression> list = exprs.list();
		ast.Expression expr = ident;
		for(int i=0; i<list.size(); i++) {
			expr = new ast.Selector(expr, list.get(i));
		}
		yyValue = expr;
	}
	;

ast.Expression Factor = 
 	  IdentifierSelector
 	/ void:"(" void:Spacing Expression void:Spacing void:")"
	/ Integer Spacing
	;

//FIXME: try to clean this up somehow
constant Action<ast.Expression> FactorTail =
	void:Spacing op:("*" / "DIV" / "MOD" / "&") void:Spacing rhs:Factor
		{
          yyValue = new Action<ast.Expression>() {
            public ast.Expression run(ast.Expression lhs) {
            	ast.Expression result = null;
            	if (op.equals("*")) result = new ast.MulExpression(lhs, rhs);
            	if (op.equals("DIV")) result = new ast.DivExpression(lhs, rhs);
            	if (op.equals("MOD")) result = new ast.ModExpression(lhs, rhs);
            	if (op.equals("&")) result = new ast.AndExpression(lhs, rhs);
            	return result;
            }};
      	}
	;

ast.Expression Term =
	seed:Factor actions:FactorTail*
	{ yyValue = apply(actions, seed); }
	;

//FIXME: try to clean this up somehow
constant Action<ast.Expression> SimpleExpressionTail =
	void:Spacing op:("+" / "-") void:Spacing rhs:Term 
		{
          yyValue = new Action<ast.Expression>() {
            public ast.Expression run(ast.Expression lhs) {
            	ast.Expression result = null;
            	if (op.equals("+")) result = new ast.AddExpression(lhs, rhs);
            	if (op.equals("-")) result = new ast.SubExpression(lhs, rhs);
            	return result;
            }};
      	}
	;

ast.Expression SimpleExpression =
	seed:Term actions:SimpleExpressionTail*
		{ yyValue = apply(actions, seed); }
	/
	Term
	;

ast.Expression Expression = 
	  lhs:SimpleExpression void:Spacing op:("=" / "#" / "<=" / ">=" / "<" / ">") void:Spacing rhs:SimpleExpression
		{ yyValue = null;
		  if ("=".equals(op)) yyValue = new ast.EqExpression(lhs, rhs);
		  if ("#".equals(op)) yyValue = new ast.NeExpression(lhs, rhs);
		  if ("<".equals(op)) yyValue = new ast.LtExpression(lhs, rhs);
		  if (">".equals(op)) yyValue = new ast.GtExpression(lhs, rhs);
		  if ("<=".equals(op)) yyValue = new ast.LeExpression(lhs, rhs);
		  if (">=".equals(op)) yyValue = new ast.GeExpression(lhs, rhs);		  
		  else assert false;
		}
	/ SimpleExpression
	;
	
ast.Statement Assignment = 
	lhs:IdentifierSelector Spacing ":=" Spacing rhs:Expression
	{ yyValue = new ast.Assignment(lhs, rhs); }
	;

ast.IdentifierType IdentifierType = 
	ident:Ident void:Spacing
	{ yyValue = new ast.IdentifierType(ident); }
	;

ast.ArrayType ArrayType = 
	void:"ARRAY" void:Spacing 
	length:Expression void:Spacing
	void:"OF" void:Spacing type:Type
	{ yyValue = new ast.ArrayType(length, type); }
	;

ast.Type Type = ArrayType / IdentifierType;

Pair<String> IdentList = 
	ident1:Ident void:Spacing 
	ident2:(void:"," void:Spacing Ident void:Spacing)*
	{ yyValue = new Pair<String>(ident1, ident2); }
	;

ast.ConstDeclaration ConstDeclaration = 
	ident:Ident void:Spacing 
	void:"=" void:Spacing 
	expr:Expression void:";" void:Spacing
	{ yyValue = new ast.ConstDeclaration(ident, expr); }
	;

ast.VarDeclaration VarDeclaration = 
	idents:IdentList void:Spacing 
	void:":" void:Spacing 
	type:Type void:Spacing
	void:";" void:Spacing
	{ yyValue = new ast.VarDeclaration(idents.list(), type); }
	;

ast.Declarations Declarations = 
	consts:(void:"CONST" void:Spacing ConstDeclaration*)?
	vars:(void:"VAR" void:Spacing VarDeclaration*)?
	procs:(ProcedureDeclaration void:";" void:Spacing)*
	{ 	List<ast.Declaration> decls = new ArrayList<ast.Declaration>();
		if (consts != null) consts.addTo(decls);
		if (vars != null) vars.addTo(decls);
		if (procs != null) procs.addTo(decls);
		yyValue = new ast.Declarations(decls); 
	}
	;

List<ast.Parameter> FPSection = 
	var:"VAR"? Spacing names:IdentList Spacing ":" Spacing type:Type
	{ 
		List<ast.Parameter> list = new ArrayList<ast.Parameter>();
		boolean byRef = ("VAR".equals(var));
		for(int i=0; i<names.size(); i++) {
			list.add(new ast.Parameter(names.get(i), type, byRef));
		}
		yyValue = list;
	}
	;

List<ast.Parameter> FormalParameters = 
	  void:"(" void:Spacing void:")" yyValue:null
	/ void:"(" args1:FPSection args2:(void:";" FPSection)* ")"
	{ 	List<ast.Parameter> params = new ArrayList<ast.Parameter>();
		if (args1 != null) params.addAll(args1);
		if (args2 != null) {
			for(List<ast.Parameter> l : args2.list())
				params.addAll(l);
		}
		yyValue = params;
	}
	;

ast.Procedure ProcedureDeclaration = 
	"PROCEDURE" Spacing name:Ident params:FormalParameters ";" 
	Spacing
	decl:Declarations
	Spacing
	stat:(void:"BEGIN" Spacing StatementSequence)? 
	Spacing
	"END" Spacing Ident Spacing
	{	yyValue = new ast.Procedure(name, params, decl, stat);  }
	;

public Pair<ast.Expression> ActualParameters = 
	  void:"(" void:Spacing void:")"
	/ void:"(" expr1:Expression void:Spacing expr2:(void:"," void:Spacing Expression)* void:")"
	  { yyValue = new Pair<ast.Expression>(expr1, expr2); }
	;

public ast.ProcedureCall ProcedureCall = 
	name:Ident params:ActualParameters?
	{ 	if (params == null) params = Pair.EMPTY;
		yyValue = new ast.ProcedureCall(name, params.list()); }
	;

public ast.Node ElseTail = 
	  void:"ELSE" StatementSequence
	/ void:"ELSIF" Spacing expr:Expression 
	  void:"THEN"
	  stat1:StatementSequence
	  stat2:ElseTail? 
	  { yyValue = new ast.IfStatement(expr, stat1, stat2); }
	;

public ast.IfStatement IfStatement = 
	void:"IF" Spacing expr:Expression 
	void:"THEN" 
	stat1:StatementSequence
	stat2:ElseTail?
	void:"END"
	{ yyValue = new ast.IfStatement(expr, stat1, stat2); }
	;

public ast.WhileStatement WhileStatement =
	void:"WHILE" void:Spacing expr:Expression void:Spacing
	void:"DO"
	stat:StatementSequence
	void:"END"
	{ yyValue = new ast.WhileStatement(expr, stat); }
	;

public ast.Statement Statement = 
	  Assignment Spacing 
	/ IfStatement Spacing
	/ WhileStatement Spacing
	/ ProcedureCall Spacing
	;

ast.StatementSequence StatementSequence = 
	void:Spacing
	s1:Statement
	s2:(void:Spacing void:";" void:Spacing Statement)*
	{ yyValue = new ast.StatementSequence((new Pair(s1, s2)).list()); } //FIXME
	;

public ast.Node Module = 
	Spacing 
	"MODULE" Spacing ident:Ident Spacing ";" 
	Spacing
	decl:Declarations
	stat:(void:"BEGIN" StatementSequence)?
	"END" void:Spacing void:Ident void:"."
	EndOfFile
	{  yyValue = new ast.Module(ident, decl, stat);  }
	;

